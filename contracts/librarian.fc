#include "imports/utils.fc";

global slice treasury;

() set_lib_code(cell code, int mode) impure asm "SETLIBCODE";
() change_lib(int code_hash, int mode) impure asm "CHANGELIB";

() load_data() impure inline {
    slice ds = get_data().begin_parse();
    treasury = ds~load_msg_addr();
    ds.end_parse();
}

() add_library(slice src, slice s) impure inline {
    int query_id = s~load_uint(64);
    cell code = s~load_ref();
    s.end_parse();

    throw_unless(err::access_denied, equal_slice_bits(src, treasury));

    set_lib_code(code, 2 + 16);
}

() remove_library(slice src, slice s) impure inline {
    int query_id = s~load_uint(64);
    int code_hash = s~load_uint(256);
    s.end_parse();

    throw_unless(err::access_denied, equal_slice_bits(src, treasury));

    change_lib(code_hash, 0 + 16);
}

() withdraw_surplus(slice src, slice s) impure inline {
    int query_id = s~load_uint(64);
    slice return_excess = s~load_msg_addr();
    s.end_parse();

    throw_unless(err::access_denied, equal_slice_bits(src, treasury));

    raw_reserve(fee::librarian_storage, reserve::at_most);

    builder excess = begin_cell()
        .store_uint(op::gas_excess, 32)
        .store_uint(query_id, 64);
    send_msg(false, return_excess.to_builder(), null(), excess, 0, send::unreserved_balance + send::ignore_errors);
}

() route_internal_message(int flags, slice src, slice s) impure inline {
    int op = s~load_uint(32);

    if op == op::add_library {
        return add_library(src, s);
    }

    if op == op::remove_library {
        return remove_library(src, s);
    }

    if op == op::withdraw_surplus {
        return withdraw_surplus(src, s);
    }

    if op == op::top_up {
        throw(0); ;; top up TON balance, do nothing
    }

    throw(err::invalid_op);
}

() recv_internal(cell in_msg_full, slice s) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice src = cs~load_msg_addr();

    load_data();
    route_internal_message(flags, src, s);
}
