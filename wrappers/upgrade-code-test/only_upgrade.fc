#include "../../contracts/imports/utils.fc";

global slice governor;

() load_data() impure inline_ref {
    slice ds = get_data().begin_parse();
    governor = ds~load_msg_addr();
}

() unpack_extension() impure inline_ref {
}

() upgrade_data(slice src, int query_id, cell new_data, slice return_excess) impure method_id {
    ;; Add code for upgrading data here.

    ;; This just messes with data for test.
    slice s = new_data.begin_parse();
    begin_cell()
        .store_slice(s~load_msg_addr())
        .store_ref(get_data())
        .end_cell()
        .set_data();

    ;; Do not change the following code.
    governor = null();
    load_data();
    unpack_extension();

    throw_unless(err::access_denied, equal_slice_bits(src, governor));

    builder excess = begin_cell()
        .store_uint(op::gas_excess, 32)
        .store_uint(query_id, 64);
    send_msg(false, return_excess.to_builder(), null(), excess, 0, send::remaining_value + send::ignore_errors);

    throw(0);
}

() upgrade_code(slice src, slice s) impure {
    int query_id = s~load_uint(64);
    cell new_code = s~load_ref();
    cell new_data = s~load_maybe_ref();
    slice return_excess = s~load_msg_addr();
    s.end_parse();

    throw_unless(err::access_denied, equal_slice_bits(src, governor));

    set_code(new_code);
    set_c3(new_code.begin_parse().bless());
    upgrade_data(src, query_id, new_data, return_excess);
}

() route_internal_message(int flags, slice src, slice s) impure inline {
    if flags & 1 {
        return ();
    }

    int op = s~load_uint(32);

    if op == op::upgrade_code {
        return upgrade_code(src, s);
    }

    throw(err::invalid_op);
}

() recv_internal(cell in_msg_full, slice s) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice src = cs~load_msg_addr();

    load_data();
    route_internal_message(flags, src, s);
}
